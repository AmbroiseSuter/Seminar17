/*
 * Zusammenhang fuer die Newtonsche Gravitationstheorie
 *
 * (c) 2017 Prof Dr Andreas Mueller, Hochschule Rapperswil
 */
x: [x0, x1, x2, x3];
g: matrix([ 1, 0, 0, 0],
          [ 0, 1, 0, 0],
          [ 0, 0, 1, 0],
          [ 0, 0, 0, 1]);
r: sqrt(x1^2+x2^2+x3^2);
Gamma(alpha,mu,nu) := block(
    if (alpha = 1) then return(0),
    if (mu > 1) then return(0),
    if (nu > 1) then return(0),
    -G * M * x[alpha] / (r^3)
);


ginverse: invert(g);

/*
 * Riemann-Tensor
 *
 * R^\alpha_\mu\rho\sigma
 */
Riemann(alpha,mu,rho,sigma) :=
    diff(Gamma(alpha,mu,sigma), x[rho])
   -diff(Gamma(alpha,mu,rho), x[sigma])
   +sum(Gamma(i,mu,sigma)*Gamma(alpha,i,rho), i, 1, length(g))
   -sum(Gamma(i,mu,rho)*Gamma(alpha,i,sigma), i, 1, length(g));

ratsimp(Riemann(2,1,2,3));
ratsimp(Riemann(2,1,2,1));
ratsimp(Riemann(2,1,3,1));
ratsimp(Riemann(2,2,3,2));

Ricci(alpha, beta) := sum(Riemann(i, alpha, i, beta), i, 1, length(g));

ratsimp(Ricci(1,1));
Ricci(1,2);
Ricci(1,3);
Ricci(1,4);
Ricci(2,2);
Ricci(2,3);
Ricci(2,4);
Ricci(3,3);
Ricci(3,4);
Ricci(4,4);

RicciScalar() :=
    sum(
        sum(ginverse[i, j] * Ricci(i, j), i, 1, length(g)),
      j, 1, length(g));

/*
 * Einstein-Tensor
 *
 * G_\alpha\beta = R_\alpha\beta - 1/2 g_\mu\nu R
 */
Einstein(alpha,beta) :=
     Ricci(alpha,beta) - 1/2 * RicciScalar() * g[alpha, beta];

ratsimp(Einstein(1,1));
ratsimp(Einstein(1,2));
ratsimp(Einstein(1,3));
ratsimp(Einstein(1,4));
ratsimp(Einstein(2,2));
ratsimp(Einstein(2,3));
ratsimp(Einstein(2,4));
ratsimp(Einstein(3,3));
ratsimp(Einstein(3,4));
ratsimp(Einstein(4,4));

